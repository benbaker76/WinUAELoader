; $Id: Install 1.4 2011/08/14 23:44:25 wepl Exp wepl $
;----------------------------
; Check version and install if newer
; IN:	#source-file
;	#dest-file

(procedure P_install
  (set #sver (getversion #source-file))
  (if
    (exists #dest-file)
    (set #dver (getversion #dest-file))
    (set #dver 0)
  )
  (if
    (< #sver #dver)
    (abort
      "The installation has determinated that the already installed program "
      "\"" #dest-file "\" has a newer version than the program to install. "
      "The current WHDLoad package seems to be outdated. Therefore the "
      "installation will be canceled. Please try to get a newer version of "
      "the WHDLoad package."
    )
  )
  (set #prompt
    (
      "\nNow installing the program\n\n\"%s\" with version %ld.%ld\n\n"
      (fileonly #source-file) (/ #sver 65536) (BITAND #sver 65535)
    )
  )
  (if
    (> #dver 0)
    (set #prompt 
      (
        "%sby overwriting\n\n\"%s\" with version %ld.%ld"
        #prompt #dest-file (/ #dver 65536) (BITAND #dver 65535)
      )
    )
    (set #prompt ("%sinto the directory\n\n\"%s\"" #prompt (pathonly #dest-file)))
  )
  (if
    (= @user-level 2)
    (set #choice
      (askbool
        (prompt #prompt)
        (default 1)
        (choices "Install" "Skip")
        (help @askbool-help)
      )
    )
    (set #choice 1)
  )
  (if
    (= #choice 1)
    (copyfiles
      (help @copyfiles-help)
      (source #source-file)
      (dest (pathonly #dest-file))
      (newname (fileonly #dest-file))
    )
  )
)

;----------------------------

(if
  (exists "C/InstallBB")
  (set #mode "dev")
  (set #mode "usr")
)

(if
  (= #mode "dev")
  (welcome
    "Welcome to the WHDLoad installation.\n"
    "This script will copy all files which need a special location to their recommend places.\n"
    "All other files like the source examples and the Install Templates will not copied."
    "Therefore it is recommend to move this drawer yourself to your prefered location.\n"
    "If you don't want to install all provided programs, please select the expert mode where you can skip these."
  )
  (welcome
    "Welcome to the WHDLoad installation."
  )
)

(set @default-dest
  (askdir
    (prompt "Where should WHDLoad and the other tools installed ?\nThe location must be reachable via the path !\nRecommend is \"C:\".")
    (help @askdir-help)
    (default "C:")
    (disk)
  )
)

(set #path "C")
(foreach #path "#?"
  (
    (set #source-file (tackon #path @each-name))
    (set #dest-file   (tackon @default-dest @each-name))
    (P_install)
  )
)

(procedure P_InstallPrefs
  (set #IP_src (tackon "S" #prefs))
  (set #IP_dst (tackon "S:" #prefs))
  (if
    (exists #IP_dst)
    (
      (set #IP_srcsum (getsum #IP_src))
      (if
	(<> #IP_srcsum (getsum #IP_dst))
	(
	  (set #IP_newname ("%s.default" #prefs))
	  (set #IP_dst ("S:%s" #IP_newname))
	  (if
	    (exists #IP_dst)
	    (if
	      (= #IP_srcsum (getsum #IP_dst))
	      (set #IP_copy 0)
	      (set #IP_copy 1)
	    )
	    (set #IP_copy 1)
	  )
	  (if
	    (= #IP_copy 1)
	    (
	      (copyfiles
		(help @copyfiles-help)
		(source #IP_src)
		(dest "S:")
		(newname #IP_newname)
	      )
	      (message
		"\nThere was already a WHDLoad preferences files installed. Therefore the new"
		" one has been installed as \"" #IP_dst "\". You may check if there are"
		" useful new options for you and adapt your existing preferences file."
	      )
	    )
	  )
	)
      )
    )
    (copyfiles
      (help @copyfiles-help)
      (source #IP_src)
      (dest "S:")
    )
  )
)
(set #prefs "WHDLoad.prefs") (P_InstallPrefs)
(set #prefs "WHDLoad-Startup") (P_InstallPrefs)
(set #prefs "WHDLoad-Cleanup") (P_InstallPrefs)

(if
  (= #mode "dev")
  (
    (set #dest
      (askdir
        (prompt "Where should the include files installed ?")
        (help @askdir-help)
        (default "Includes:")
      )
    )
    (set #path "include")
    (foreach #path "#?"
      (copyfiles
        (help @copyfiles-help)
        (source (tackon #path @each-name))
        (dest #dest)
      )
    )
    (set #dest
      (askdir
        (prompt "Where should the autodoc file installed ?")
        (help @askdir-help)
        (default "Autodocs:")
      )
    )
    (copyfiles
      (help @copyfiles-help)
      (source "autodoc/whdload.doc")
      (dest #dest)
    )
    (exit
      "\n"
      "Installation is complete.\n"
      "\n"
      "All files which need a special location has been copied.\n"
      "Please move all other files yourself to your prefered place.\n"
      "\n"
      "Hope WHDLoad and the tools will be useful for you.\n"
      "\n"
      "Bye Bert."
      (quiet)
    )
  )
  (
    (set #dest
      (askdir
        (prompt "Where should the documentation for WHDLoad installed ?\nA drawer WHDLoad will be created there.")
        (help @askdir-help)
        (default "Help:")
      )
    )
    (copyfiles
      (help @copyfiles-help)
      (source "Docs")
      (dest (tackon #dest "WHDLoad"))
      (all)
      (infos)
    )
    (exit
      "\n"
      "Installation is now complete.\n"
      "Please read the documentation carefully on how to use and configure WHDLoad for your needs."
      (quiet)
    )
  )
)


